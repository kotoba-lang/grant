(ns grant.surface-test
  (:require [grant.surface :as surface]
            [clojure.test :refer [deftest is testing]]))

(deftest known-surfaces-offer-caps-and-unknown-is-nil
  (is (contains? (surface/offered-by-id "robot") :topic/publish))
  (is (contains? (surface/offered-by-id "browser") :dom/render))
  (is (not (contains? (surface/offered-by-id "browser") :topic/publish)))
  (is (nil? (surface/offered-by-id "teapot")))
  (is (and (surface/is-known? "cloud") (not (surface/is-known? "teapot")))))

(deftest offered-set-matches-the-registered-providers
  (let [browser (surface/browser)
        offered (surface/offered browser)]
    (doseq [cap offered]
      (is (some? (surface/provider-for-cap browser cap))))
    (is (= "dom-render" (:aiueos.surface/name (surface/provider-for-cap browser :dom/render))))
    (is (= "dom-event" (:aiueos.surface/name (surface/provider-for-cap browser :dom/event))))
    (is (= :framebuffer/present
           (:aiueos.surface/cap (surface/provider-by-name browser "fb-present"))))
    (is (= offered (surface/offered-by-id "browser")))))

(deftest a-capability-can-have-multiple-host-imports
  (let [robot (surface/robot)]
    (is (contains? (surface/offered robot) :topic/subscribe))
    (is (= :topic/subscribe (:aiueos.surface/cap (surface/provider-by-name robot "poll"))))
    (is (= :topic/subscribe (:aiueos.surface/cap (surface/provider-by-name robot "take"))))
    (is (= :topic/subscribe (:aiueos.surface/cap (surface/provider-by-name robot "count")))))
  (let [cloud (surface/cloud)]
    (is (contains? (surface/offered cloud) :storage/kv))
    (is (= :storage/kv (:aiueos.surface/cap (surface/provider-by-name cloud "kv-set"))))
    (is (= :storage/kv (:aiueos.surface/cap (surface/provider-by-name cloud "kv-get"))))))

(deftest a-surface-does-not-offer-another-surfaces-caps
  (is (nil? (surface/provider-for-cap (surface/robot) :dom/render)))
  (is (nil? (surface/provider-for-cap (surface/browser) :pci/config)))
  (is (nil? (surface/provider-for-cap (surface/cloud) :dom/event))))

(deftest component-v03-providers-are-named-per-surface
  (is (= "http-get-stream"
         (:aiueos.surface/name
          (surface/provider-for-cap (surface/browser) :http/get-stream))))
  (let [cloud (surface/cloud)]
    (doseq [[cap provider-name]
            [[:http/get-stream "http-get-stream"]
             [:object/get-stream "object-get-stream"]
             [:object/put-block "object-put-block"]
             [:object/compare-and-set-ref "object-compare-and-set-ref"]]]
      (is (= provider-name
             (:aiueos.surface/name (surface/provider-for-cap cloud cap))))))
  (is (nil? (surface/provider-for-cap (surface/robot) :http/get-stream)))
  (is (nil? (surface/provider-for-cap (surface/browser) :object/get-stream))))

(deftest computer-virtual-backs-synthetic-input-but-not-the-host-hid
  (let [v (surface/computer-virtual)]
    (is (some? (surface/provider-for-cap v :pointer/move)))
    (is (some? (surface/provider-for-cap v :keyboard/type)))
    (is (some? (surface/provider-for-cap v :display/frame)))
    (is (nil? (surface/provider-for-cap v :pointer/host)))
    (is (nil? (surface/provider-for-cap v :keyboard/host)))
    (is (nil? (surface/provider-for-cap v :display/host)))
    (is (= (surface/offered (surface/computer-vm)) (surface/offered v)))
    (let [h (surface/computer-host)]
      (is (some? (surface/provider-for-cap h :pointer/host)))
      (is (some? (surface/provider-for-cap h :pointer/move))))
    (is (and (surface/is-known? "computer-virtual")
             (surface/is-known? "computer-vm")
             (surface/is-known? "computer-host")))))

(deftest union-composes-offered-sets-with-self-winning
  (let [edge (surface/union (surface/robot) (surface/cloud))]
    (is (contains? (surface/offered edge) :topic/publish))
    (is (contains? (surface/offered edge) :storage/kv))
    (is (= "robot+cloud" (:aiueos.surface/id edge)))
    (is (= "log" (:aiueos.surface/name (surface/provider-for-cap edge :log/write))))))
