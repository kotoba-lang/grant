(ns grant.net-test
  (:require [grant.net :as net]
            [grant.policy :as policy]
            [clojure.test :refer [deftest is testing]]))

(deftest guarded-fetch-never-calls-fn-when-denied
  (let [called (atom false)
        pol {:aiueos.policy/net-allow #{"good.example"}}
        out (net/guarded-fetch pol "https://evil.example/x"
                               (fn [_] (reset! called true) :should-not-run))]
    (is (false? @called))
    (is (false? (:ok? out)))
    (is (= :net-url-not-allowed (:aiueos.net/denied out)))))

(deftest guarded-fetch-runs-when-allowed
  (let [pol {:aiueos.policy/net-allow #{"good.example"}}
        out (net/guarded-fetch pol "https://good.example/v1"
                               (fn [u] {:echo u}))]
    (is (true? (:ok? out)))
    (is (= {:echo "https://good.example/v1"} (:aiueos.net/result out)))))

(deftest fixture-fetch-respects-allowlist
  (let [pol (policy/parse-policy {:aiueos/net-allow #{"https://api/health"}})
        fixtures {"https://api/health" "ok"
                  "https://other/" "nope"}]
    (is (= 200 (:status (:aiueos.net/result
                         (net/fixture-fetch pol fixtures "https://api/health")))))
    (is (false? (:ok? (net/fixture-fetch pol fixtures "https://other/"))))))
